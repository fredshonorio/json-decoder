
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def NAME = "json-decoder"
def GITHUB = "https://github.com/fredshonorio/json-decoder"
def GITHUB_GIT = "https://github.com/fredshonorio/json-decoder.git"
def USER = properties.getProperty("bintray.user")

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "fredshonorio"
            name "Fred Hon√≥rio"
            email "fredshonorio@gmail.com"
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.fredhonorio'
            artifactId 'json-decoder'
            version getVersion()
            pom.withXml {
                def root = asNode()
                root.appendNode('name', NAME)
                root.appendNode('url', GITHUB)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = USER
    key = properties.getProperty("bintray.apikey")
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = NAME
        userOrg = "fredhonorio-com"
        licenses = ['Apache-2.0']
        vcsUrl = GITHUB_GIT
    }
}
